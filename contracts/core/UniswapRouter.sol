// Copyright (C) 2020 Zerion Inc. <https://zerion.io>
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program. If not, see <https://www.gnu.org/licenses/>.
//
// SPDX-License-Identifier: LGPL-3.0-only

pragma solidity 0.7.3;
pragma experimental ABIEncoderV2;

import { UniswapV2Router02 } from "../interfaces/UniswapV2Router02.sol";
import { Logger } from "./Logger.sol";

enum RouterType { Uniswap, SushiSwap }

contract UniswapRouter is Logger {
    address internal constant UNISWAP_ROUTER = 0x7a250d5630B4cF539739dF2C5dAcb4c659F2488D;
    address internal constant SUSHISWAP_ROUTER = 0xd9e1cE17f2641f24aE83637ab66a2cca9C378B9F;
    address internal constant ETH = 0xEeeeeEeeeEeEeeEeEeEeeEEEeeeeEeeeeeeeEEeE;

    function swapExactTokensForTokens(
        RouterType routerType,
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external {
        emit TokenTransfer(path[0], msg.sender, amountIn);
        swap(
            routerType,
            abi.encodeWithSelector(
                UniswapV2Router02.swapExactTokensForTokens.selector,
                amountIn,
                amountOutMin,
                path,
                to,
                deadline
            )
        );
    }

    function swapExactETHForTokens(
        RouterType routerType,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external payable {
        emit TokenTransfer(ETH, msg.sender, msg.value);
        swap(
            routerType,
            abi.encodeWithSelector(
                UniswapV2Router02.swapExactETHForTokens.selector,
                amountOutMin,
                path,
                to,
                deadline
            )
        );
    }

    function swapExactTokensForETH(
        RouterType routerType,
        uint256 amountIn,
        uint256 amountOutMin,
        address[] calldata path,
        address to,
        uint256 deadline
    ) external {
        emit TokenTransfer(path[0], msg.sender, amountIn);
        swap(
            routerType,
            abi.encodeWithSelector(
                UniswapV2Router02.swapExactTokensForETH.selector,
                amountIn,
                amountOutMin,
                path,
                to,
                deadline
            )
        );
    }

    function swap(RouterType routerType, bytes memory callData) internal {
        address router = routerType == RouterType.Uniswap ? UNISWAP_ROUTER : SUSHISWAP_ROUTER;
        // solhint-disable-next-line avoid-low-level-calls
        (bool success, bytes memory returnData) = router.delegatecall(callData);

        // assembly revert opcode is used here as `returnData`
        // is already bytes array generated by the callee's revert()
        // solhint-disable-next-line no-inline-assembly
        assembly {
            if eq(success, 0) {
                revert(add(returnData, 32), returndatasize())
            }
        }
    }
}
